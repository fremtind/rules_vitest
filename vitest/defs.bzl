"""# Public API for Vitest rules
"""

load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:directory_path.bzl", "directory_path")
load("@aspect_bazel_lib//lib:utils.bzl", "default_timeout", "to_label")
load("//vitest/private:vitest_test.bzl", vitest_test_rule = "vitest_test")

UPDATE_SNAPSHOTS_TARGET_SUFFIX = "_update_snapshots"

def _vitest_from_node_modules(vitest_rule, name, node_modules, auto_configure_reporters, coverage_provider, **kwargs):
    data = kwargs.pop("data", [])

    vitest_dep = "{}/vitest".format(node_modules)
    vitest_coverage_dep = "{}/@vitest/coverage-{}".format(node_modules, coverage_provider)

    if vitest_dep not in data:
        data.append(vitest_dep)

    if auto_configure_reporters and vitest_coverage_dep not in data:
        data.append(vitest_coverage_dep)

    vitest_rule(
        auto_configure_reporters = auto_configure_reporters,
        coverage_provider = coverage_provider,
        name = name,
        enable_runfiles = select({
            "@aspect_bazel_lib//lib:enable_runfiles": True,
            "//conditions:default": False,
        }),
        data = data,
        testonly = True,
        **kwargs
    )

def vitest_test(
        name,
        node_modules,
        config = None,
        data = [],
        snapshots = False,
        auto_configure_reporters = True,
        auto_configure_test_sequencer = True,
        coverage_provider = "istanbul",
        snapshots_ext = ".snap",
        quiet_snapshot_updates = False,
        timeout = None,
        size = None,
        **kwargs):
    """vitest_test rule

    Supports Bazel sharding. See https://docs.bazel.build/versions/main/test-encyclopedia.html#test-sharding.

    Args:
        name: A unique name for this target.

        node_modules: Label pointing to the linked node_modules target where vitest is linked, e.g. `//:node_modules`.
            The coverage provider package (`@vitest/coverage-istanbul` or `@vitest/coverage-v8`) is also required
            when `auto_configure_reporters` is True. The specific package depends on the `coverage_provider` parameter.
            By default, `@vitest/coverage-istanbul` is used.

            NB: Only the required npm packages are included in data from `//:node_modules`. Other npm packages
            are not included as inputs.

        config: "Optional Vitest config file. See https://vitest.dev/config/file.html.

            Supported config file types are ".js", ".cjs", ".mjs" which come from https://vitest.dev/config/file.html
            minus TypeScript since we this rule extends from the configuration. TypeScript vitest configs should be transpiled
            before being passed to vitest_test with [rules_ts](https://github.com/aspect-build/rules_ts).

        data: Runtime dependencies of the Vitest test.

            This should include all test files, configuration files & files under test.

        snapshots: If True, a `{name}_update_snapshots` binary target is generated that will update all existing `__snapshots__`
            directories when `bazel run`. This is the equivalent to running `vitest -u` or `vitest --update` outside of Bazel,
            except that new `__snapshots__` will not automatically be created on update. To bootstrap a new `__snapshots__` directory,
            you can create an empty one and then run the `{name}_update_snapshots` target to populate it.

            If the name of the snapshot directory is not the default `__snapshots__` because of a custom snapshot resolver,
            you can specify customize the snapshot directories with a `glob` or a static list. For example,

            ```
            vitest_test(
                name = "test",
                node_modules = "//:node_modules",
                config = "vitest.config.mjs",
                data = [
                    "greetings/greetings.jsx",
                    "greetings/greetings.test.jsx",
                    "link/link.jsx",
                    "link/link.test.jsx",
                ],
                snapshots = glob(["**/__snaps__"], exclude_directories = 0),
            )
            ```

            or with a static list,

            ```
                snapshots = [
                    "greetings/__greetings_snaps__",
                    "link/__link_snaps__",
                ]
            ```

            Snapshots directories must not contain any files except for snapshots. There must also be no BUILD files in
            the snapshots directories since they must be part of the same Bazel package that the `vitest_test` target is in.

            If snapshots are _not_ configured to output to a directory that contains only snapshots, you may alternately
            set `snapshots` to a list of snapshot files expected to be generated by this `vitest_test` target.
            These must be source files and all snapshots that are generated must be explicitly listed. You may use a
            `glob` such as `glob(["**/*.snap"])` to generate this list, in which case all snapshots must already be on
            disk so they are discovered by `glob`.

        auto_configure_reporters: Let vitest_test configure reporters for Bazel test and xml test logs.

            The `default` reporter is used for the standard test log and `junit` is used for the xml log.
            These reporters are appended to the list of reporters from the user Vitest `config` only if they are
            not already set.

        auto_configure_test_sequencer: Let vitest_test configure a custom test sequencer for Bazel test that support Bazel sharding.

            Any custom test.sequence.sequencer value in a user Vitest `config` will be overridden.

            See https://vitest.dev/config/#sequence-sequencer for more information on Vitest test.sequence.sequencer config option.

        coverage_provider: The coverage provider to use. Valid values are "istanbul" (default) or "v8".
            This determines which coverage package dependency is automatically added: either `@vitest/coverage-istanbul`
            or `@vitest/coverage-v8` when `auto_configure_reporters` is True.

        snapshots_ext: The expected extensions for snapshot files. Defaults to `.snap`, the Vitest default.

        quiet_snapshot_updates: When True, snapshot update stdout & stderr is hidden when the snapshot update is successful.

            On a snapshot update failure, its stdout & stderr will always be shown.

        timeout: standard attribute for tests. Defaults to "short" if both timeout and size are unspecified.

        size: standard attribute for tests

        **kwargs: Additional named parameters passed to both `js_test` and `js_binary`.
            See https://github.com/aspect-build/rules_js/blob/main/docs/js_binary.md
    """
    tags = kwargs.pop("tags", [])

    snapshot_data = []

    if snapshots == True:
        snapshots = native.glob(["**/__snapshots__"], exclude_directories = 0, allow_empty = True)

    if type(snapshots) == "string":
        snapshot_data = native.glob(["{}/**".format(snapshots)], allow_empty = True)
    elif type(snapshots) == "list":
        for snapshot in snapshots:
            snapshot_label = to_label(snapshot)
            if snapshot_label.package != native.package_name():
                msg = "Expected vitest_test '{target}' snapshots to be in test target package '{vitest_test_package}' but got '{snapshot_label}' in package '{snapshot_package}'".format(
                    vitest_test_package = native.package_name(),
                    snapshot_label = snapshot_label,
                    snapshot_package = snapshot_label.package,
                    target = to_label(name),
                )
                fail(msg)
            if snapshot_label.name.endswith(snapshots_ext):
                snapshot_data.append(snapshot_label)
            else:
                snapshot_data.extend(native.glob(["{}/**".format(snapshot)], allow_empty = True))

    elif snapshots != False and snapshots != None:
        msg = "snapshots expected to be a boolean, string or list but got {}".format(snapshots)
        fail(msg)

    entry_point = "_{}_vitest_entrypoint".format(name)
    directory_path(
        name = entry_point,
        directory = "{}/vitest/dir".format(node_modules),
        path = "vitest.mjs",
    )

    bazel_sequencer = "_{}_bazel_sequencer".format(name)
    copy_file(
        name = bazel_sequencer,
        src = "@fremtind_rules_vitest//vitest/private:bazel_sequencer.mjs",
        out = "_{}_bazel_sequencer.mjs".format(name),
        visibility = ["//visibility:public"],
    )

    bazel_snapshot_reporter = "_{}_bazel_snapshot_reporter".format(name)
    copy_file(
        name = bazel_snapshot_reporter,
        src = "@fremtind_rules_vitest//vitest/private:bazel_snapshot_reporter.cjs",
        out = "_{}_bazel_snapshot_reporter.cjs".format(name),
        visibility = ["//visibility:public"],
    )

    bazel_snapshot_resolver = "_{}_bazel_snapshot_resolver".format(name)
    copy_file(
        name = bazel_snapshot_resolver,
        src = "@fremtind_rules_vitest//vitest/private:bazel_snapshot_resolver.mjs",
        out = "_{}_bazel_snapshot_resolver.mjs".format(name),
        visibility = ["//visibility:public"],
    )

    # This is the primary {name} vitest_test test target
    _vitest_from_node_modules(
        vitest_rule = vitest_test_rule,
        name = name,
        node_modules = node_modules,
        config = config,
        data = data + snapshot_data,
        auto_configure_reporters = auto_configure_reporters,
        auto_configure_test_sequencer = auto_configure_test_sequencer,
        coverage_provider = coverage_provider,
        tags = tags,
        size = size,
        bazel_sequencer = bazel_sequencer,
        bazel_snapshot_reporter = bazel_snapshot_reporter,
        bazel_snapshot_resolver = bazel_snapshot_resolver,
        timeout = default_timeout(size, timeout),
        entry_point = entry_point,
        **kwargs
    )

    if snapshots != False:
        _vitest_from_node_modules(
            vitest_rule = vitest_test_rule,
            name = name + UPDATE_SNAPSHOTS_TARGET_SUFFIX,
            node_modules = node_modules,
            config = config,
            # Also pass data to the tool for vitest snapshot updates incase there are load bearing
            # data deps the config requires
            data = data,
            auto_configure_reporters = auto_configure_reporters,
            auto_configure_test_sequencer = auto_configure_test_sequencer,
            coverage_provider = coverage_provider,
            update_snapshots = True,
            quiet_snapshot_updates = quiet_snapshot_updates,
            entry_point = entry_point,
            bazel_sequencer = bazel_sequencer,
            bazel_snapshot_reporter = bazel_snapshot_reporter,
            bazel_snapshot_resolver = bazel_snapshot_resolver,
            tags = tags + ["manual"],  # tagged manual so it is not built unless the {name}_update_snapshot target is run
            **kwargs
        )
